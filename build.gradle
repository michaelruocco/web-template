buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.samueltbrown:gradle-cucumber-plugin:0.9"
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
        classpath 'org.flywaydb:flyway-gradle-plugin:4.0'
        classpath 'com.github.michaelruocco.mysql:embedded-mysql-plugin:0.4.0'
        classpath 'com.sourcemuse.gradle.plugin:gradle-mongo-plugin:0.11.0'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'embedded-mysql'
apply plugin: 'mongo'
apply plugin: 'com.github.samueltbrown.cucumber'

group 'com.github.michaelruocco'
version '1.0.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://central.maven.org/maven2" }
}

dependencies {
    compile 'org.springframework:spring-webmvc:4.2.2.RELEASE'
    compile 'org.springframework:spring-jdbc:4.2.2.RELEASE'
    compile 'javax.servlet:jstl:1.2'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'mysql:mysql-connector-java:5.1.37'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.1.2'
    compile 'org.springframework.data:spring-data-mongodb:1.8.1.RELEASE'
    compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-log4j12:1.7.13'
    compile 'io.springfox:springfox-swagger2:2.4.0'
    compile 'io.springfox:springfox-swagger-ui:2.4.0'

    testCompile 'org.assertj:assertj-core:3.3.0'
    testCompile 'junit:junit:4.11'
    testCompile 'info.cukes:cucumber-java:1.2.4'
    testCompile 'info.cukes:cucumber-junit:1.2.4'
    testCompile 'org.apache.httpcomponents:httpclient:4.5.1'
    testCompile 'com.google.code.gson:gson:2.5'

    tomcat 'org.apache.tomcat:tomcat-catalina:7.0.59'
    tomcat 'org.apache.tomcat:tomcat-coyote:7.0.59'
    tomcat 'org.apache.tomcat:tomcat-jasper:7.0.59'
}

Properties props = new Properties()
props.load(new FileInputStream('src/main/resources/local.properties'))
URI jdbcUri = URI.create(props.getProperty("jdbc.url").substring(5));

flyway {
    url = props.getProperty('jdbc.url')
    user = props.getProperty('jdbc.username')
    password = props.getProperty('jdbc.password')
}

embeddedMysql {
    databaseName = jdbcUri.getPath().substring(1);
    version = 'v5_6_22'
    port = jdbcUri.getPort()
}

task startTomcat(type: com.bmuschko.gradle.tomcat.tasks.TomcatRunWar) {
    daemon = false
}

task startTomcatDaemon(type: com.bmuschko.gradle.tomcat.tasks.TomcatRunWar) {
    daemon = true
}

task startWebApp {
    dependsOn clean, build, startEmbeddedMysql, flywayMigrate, startTomcat
    build.mustRunAfter clean
    startEmbeddedMysql.mustRunAfter build
    flywayMigrate.mustRunAfter startEmbeddedMysql
    startTomcat.mustRunAfter flywayMigrate
}

task cucumberSetup {
    dependsOn clean, build, startEmbeddedMysql, flywayMigrate, startTomcatDaemon
    build.mustRunAfter clean
    startEmbeddedMysql.mustRunAfter build
    flywayMigrate.mustRunAfter startEmbeddedMysql
    startTomcatDaemon.mustRunAfter flywayMigrate
}