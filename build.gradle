plugins {
    id 'java-library'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.michaelruocco.embedded-mysql-plugin' version '2.1.11'
}

testSets {
    cucumberTest
}

group 'com.github.michaelruocco'
version '1.0.0'

repositories {
    mavenCentral()
    maven { url "http://central.maven.org/maven2" }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-dependencies:2.3.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'mysql:mysql-connector-java:8.0.20'
    implementation 'org.springdoc:springdoc-openapi-ui:1.4.2'
    implementation 'org.flywaydb:flyway-core:6.5.0'

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.12'
    testImplementation 'com.google.code.gson:gson:2.8.6'

    testImplementation 'io.cucumber:cucumber-java:6.1.2'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

embeddedMysql {
    url = 'jdbc:mysql://localhost:3307/template'
    version = 'v8_0_17'
}

task cucumber() {
    dependsOn assemble, cucumberTestClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.cucumberTest.output
            args = ['--plugin', 'pretty', '--glue', 'uk.co.mruoc', 'src/cucumberTest/resources']
        }
    }
}

bootRun.dependsOn startEmbeddedMysql
bootRun.finalizedBy stopEmbeddedMysql